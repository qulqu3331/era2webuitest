;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SYSTEM_TRAIN.ERB
;	Facility	:TRAIN関連処理。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- XXXX
;
;	・ SYSTEM_TRAIN						SYSTEM:TRAIN
;	・ TRAIN_INIT						TR:初期化
;	・ TRAIN_SEC_START					TR:前処理
;	・ TRAIN_SEC_MAIN					TR:主処理
;	・ TRAIN_SEC_END					TR:後処理
;	・ TRAIN_SHOW_COM_MAIN				TR:コマンドリスト表示(主処理)
;	・ TRAIN_SHOW_COM_CR				TR:コマンドリスト改行処理
;
; *- 処理系(詳細)
;
;	・ TRAIN_COM_EXE					TR:コマンド実行
;	・ TR_CALC_MOOD						TR:ムード
;	・ TR_LOST_VIRGIN					TR:初体験
;	・ TR_SAME_COMMAND					TR:同一コマンド
;	・ TR_MSG_START						TR:開始メッセージ
;	・ TR_MSG_END						TR:終了メッセージ
;	・ TR_LOG							TR:履歴
;	・ TR_AUTO_COM						TR:自動コマンド
;	・ TR_START_SET						TR:開始状態セット
;
;===============================================================================


;-------------------------------------------------------------------------------
;   XXXX
;-------------------------------------------------------------------------------

;=================================================
;   sub SYSTEM_TRAIN
;=================================================
;   SYSTEM:TRAIN
;-------------------------------------------------
@SYSTEM_TRAIN
#DIM L_COM

;- TR:初期化
CALL TRAIN_INIT

;- TR:開始状態セット
CALL TR_START_SET

;- TR:開始メッセージ
CALL TR_MSG_START

;-----------------------------
;- 処理開始
;-----------------------------

WHILE 1

	;- TR:前処理
	CALL TRAIN_SEC_START

	;- TR:主処理
	CALL TRAIN_SEC_MAIN

	;- TR:後処理
	CALL TRAIN_SEC_END

	SIF SELECTCOM == [[TRAIN:行動終了]]
		BREAK

WEND

;-----------------------------
;- 処理終了
;-----------------------------

;- TR:終了メッセージ
CALL TR_MSG_END

;- TR:反映処理
CALL TR_REFLECT_MAIN

DRAWLINE

;- ABLUP:自動(主処理)
CALL ABLUP_AUTO_MAIN

;- TR:MARKUP(メイン)
CALL TR_MARKUP_MAIN

;- TR:パラメータ変動(既成事実)
CALL TR_PALAMCHG_FACT_MAIN

FLAG:行動フェーズ = [[行動フェーズ：交流]]

;- VAR:リスト更新
CALL VAR_MAKE_LIST

;- TR:パラメータ変動(終了時)
CALL TR_PALAMCHG_AFTER

;- TR:初期化
CALL TRAIN_INIT


;=================================================
;   sub TRAIN_INIT
;=================================================
;   TR:初期化
;-------------------------------------------------
; Input:
;  ARG					モード(0:交流 1:調教)
;-------------------------------------------------
@TRAIN_INIT(ARG=0)

;- 交流
IF !ARG
	VARSET TFLAG
	VARSET TSTR
	VARSET PREVCOM
	VARSET NEXTCOM
	FLAG:行動フェーズ = [[行動フェーズ：交流]]

	VARSET GTR_JUELUP_BK:0:0

	;- 全キャラ
	REPEAT CHARANUM
		;- 全要素を初期化
		VARSET GOTJUEL:COUNT:0, 0
		VARSET TEQUIP:COUNT:0, 0
		VARSET EX:COUNT:0, 0

		VARSET PALAM:COUNT:0, 0
		VARSET SOURCE:COUNT:0, 0
		VARSET CUP:COUNT:0, 0
		VARSET CDOWN:COUNT:0, 0
		VARSET DOWNBASE:COUNT:0, 0

		VARSET TCVAR:COUNT:0, 0

		;- その他
		IF COUNT == MASTER
			BASE:COUNT:体力 = MAXBASE:COUNT:体力
		ELSE
			BASE:COUNT:体力 = ( MAXBASE:COUNT:体力 - 100 ) + 10
		ENDIF
		BASE:COUNT:絶頂 = 0
		BASE:COUNT:ムード = 0

		;TR:滞在行動設定
		CALL TR_STAY_ACTION_SET(COUNT, 0)
	REND

	;- TR_MAP:滞在エリア設定
	CALL TR_MAP_STAY_AREA_SET
;- 調教
ELSE
	FLAG:行動フェーズ = [[行動フェーズ：調教]]
	TFLAG:行動経過ターン数 = 0
	;- 全キャラ
	REPEAT CHARANUM
		SIF !CFLAG:COUNT:行動対象フラグ
			CONTINUE

		BASE:COUNT:体力 = MAXBASE:COUNT:体力
		TCVAR:COUNT:遭遇フラグ = 1
		TCVAR:COUNT:休憩フラグ = 0

		;TR:滞在行動設定
		CALL TR_STAY_ACTION_SET(COUNT, 0)
	REND
ENDIF

;- 共通
REPEAT CHARANUM
	CFLAG:COUNT:調教疲労フラグ = 0

	;TR:滞在行動設定
	CALL TR_STAY_ACTION_SET(COUNT, 1)
REND

IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
	TFLAG:行動最大ターン数 = ( MAXBASE:MASTER:体力 / 10 )
ELSE
	TFLAG:行動最大ターン数 = ( MAXBASE:MASTER:体力 / 10 ) + 60
ENDIF

;- VAR:リスト更新
CALL VAR_MAKE_LIST

;- VAR:リスト更新(通常)
CALL TR_TGT_SET_NOMAL


;=================================================
;   sub TRAIN_SEC_START
;=================================================
;   TR:前処理
;-------------------------------------------------
@TRAIN_SEC_START

;毎ターン初期化
FLAG:調教コマンド派生 = 0

;- 共通
REPEAT CHARANUM
	CSTR:COUNT:滞在行動名称 = 
	CSTR:COUNT:滞在行動内容 = 

	;TR:滞在行動設定
	CALL TR_STAY_ACTION_SET(COUNT, 1)
REND



;=================================================
;   sub TRAIN_SEC_MAIN
;=================================================
;   TR:主処理
;-------------------------------------------------
; Output:
;  RETURN				処理結果(0:続行 1:終了)
;-------------------------------------------------
@TRAIN_SEC_MAIN
#DIM  DYNAMIC L_P
#DIM  DYNAMIC L_TMP
#DIM  DYNAMIC L_PREVCOM
#DIM  DYNAMIC L_INPUT
#DIMS DYNAMIC L_ACTERR
#DIMS DYNAMIC L_ACTERR2

IF TARGET > -1
	IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
		L_ACTERR = %CALLNAME%は行動回数が0の為、行動出来ません
	ELSE
		L_ACTERR = %CALLNAME%の体力が0の為、調教出来ません
	ENDIF
ENDIF
L_ACTERR2 = %CALLNAME:MASTER%の行動回数が0です。終了 or 寝室に誘うを選んで下さい

;-----------------------------
;- 表示:ヘッダー部
;-----------------------------

;- 表示:ページヘッダ
CALL C_LINE

;- 表示:日付
CALL INFO_DATE

PRINTFORML %GET_AREA_NAME()%　　(経過ターン:{TFLAG:行動経過ターン数,2}T/{TFLAG:行動最大ターン数}T) 

;-----------------------------
;- 表示:パラメータ部
;-----------------------------

;- TR情報:上部(メイン)
CALL TR_INFO_UPPER_MAIN
PRINTL

;- TR情報:中部(メイン)
CALL TR_INFO_MIDDLE_MAIN
SIF RESULT
	GOTO COM_EXE

IF PREVCOM
	PRINTFORML ＜前回実行コマンド：%TSTR:前回実行コマンド名%＞
ELSE
	PRINTL 
ENDIF

;-----------------------------
;- コマンド受付
;-----------------------------
$INPUT_LOOP

VARSET SELECTCOM

INPUT
;- 退避
L_INPUT = RESULT

SELECTCASE L_INPUT
	;行動終了
	CASE [[TRAIN:行動終了]]
		SELECTCOM = L_INPUT
		RETURN

	;キャラ変更
	CASE 0 TO 9
		L_TMP = GTR_PL:L_INPUT

		;- CHK:ID
		SIF L_TMP < 0
			GOTO ERROR

		;- TR:ターゲット設定
		CALL TR_TGT_SET(L_TMP)

		IF TARGET == TFLAG:メインターゲット
			PREVCOM = L_PREVCOM
		ELSEIF PREVCOM
			L_PREVCOM = PREVCOM
			PREVCOM = 0
		ENDIF

	;調教メニュー
	CASE DEFTR_MACRO_COM_FROM TO DEFTR_MACRO_COM_TO

		IF !BASE:TARGET:体力
			CALL C_INPUT_ERROR(@"%L_ACTERR%")
			GOTO INPUT_LOOP
		ENDIF

		IF !BASE:MASTER:体力
			CALL C_INPUT_ERROR(@"%L_ACTERR2%")
			GOTO INPUT_LOOP
		ENDIF
		LOCAL    = L_INPUT - DEFTR_MACRO_COM_FROM
		CALL COM_MACRO_EXE(LOCAL)

	;コマンド実行
	CASE 10 TO (920 -1)

		RESULT=0
		TRYCALLFORM COM_FUNC_GET_TYPE_X{L_INPUT}
		;その他以外は体力判定
		IF RESULT != [[COM種別:その他系]] 
			IF !BASE:TARGET:体力
				CALL C_INPUT_ERROR(@"%L_ACTERR%")
				GOTO INPUT_LOOP
			ENDIF
			IF !BASE:MASTER:体力
				CALL C_INPUT_ERROR(@"%L_ACTERR2%")
				GOTO INPUT_LOOP
			ENDIF
		ENDIF

		L_TMP = GTR_COMABLE:L_INPUT
		SIF L_TMP == -1
			GOTO ERROR

		SELECTCOM = L_TMP

		$COM_EXE

		;- TR:コマンド実行
		CALL TRAIN_COM_EXE

		;- 実行結果1以外の場合は、そのコマンドを中止と判断
		SIF RESULT == 0
			RESTART

		RETURN

	;能力表示
	CASE 920
		CALL INFO_STATUS_MAIN(MASTER)
	;能力表示
	CASE 921
		SIF TARGET == -1
			GOTO ERROR
		CALL INFO_STATUS_MAIN(TARGET)
	;能力表示(一覧)
	CASE 922
		CALL INFO_STATUS_LIST

	;コマンドフィルタ
	CASE 924
		;- コマンドフィルタ画面:主処理
		CALL COMMAND_FILTER_MAIN
	;コンフィグ
	CASE 925
		;表示:コンフィグ(LIST)
		CALL CONFIG_LIST
	;調教メニュー
	CASE 930 TO (930 + DEFTR_MACRO_CNT - 1)
		LOCAL = L_INPUT - 930
		;- コマンドマクロ:設定
		CALL COM_MACRO_SET(LOCAL)

	;表示系フラグ
	CASE 950 TO 957

		SELECTCASE RESULT
			CASE 950
				FLAG:フィルタ仕事 = !FLAG:フィルタ仕事
			CASE 951
				FLAG:フィルタ日常 = !FLAG:フィルタ日常
			CASE 952
				FLAG:フィルタコミュ = !FLAG:フィルタコミュ
			CASE 953
				FLAG:フィルタ愛撫 = !FLAG:フィルタ愛撫
			CASE 954
				FLAG:フィルタ道具 = !FLAG:フィルタ道具
			CASE 955
				FLAG:フィルタ奉仕 = !FLAG:フィルタ奉仕
			CASE 956
				FLAG:フィルタＶ性交 = !FLAG:フィルタＶ性交
			CASE 957
				FLAG:フィルタＡ性交 = !FLAG:フィルタＡ性交
		ENDSELECT

	;クイック移動
	CASE 960 TO 962
		IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
			;- TR:エリア移動
			CALL TR_AREA_MOVE(L_INPUT - 960 + 1)
		ELSE
			GOTO ERROR
		ENDIF

	;調教時画面表示モード
	CASE 980
		IF FLAG:調教画面表示モード < 2
			FLAG:調教画面表示モード ++
		ELSE
			FLAG:調教画面表示モード = 0
		ENDIF

	;状態解除
	CASE 985
		;- TR:部位ポイントリセット
		CALL TR_POINT_RESET(TARGET)

		;TR:滞在行動設定
		CALL TR_STAY_ACTION_SET(TARGET, 1)

	;ＭＡＧ供給方向
	CASE 986
		FLAG:ＭＡＧ供給方向 = !FLAG:ＭＡＧ供給方向

	;履歴
	CASE 992
		DRAWLINE
		PRINTFORML ＜履歴：前回のパラメータ変動＞

		;- TR:履歴
		CALL TR_LOG

	;上記以外
	CASEELSE
		$ERROR
		CALL C_INPUT_ERROR
		GOTO INPUT_LOOP
ENDSELECT
RESTART


;=================================================
;   sub TRAIN_SEC_END
;=================================================
;   TRAIN:後処理
;-------------------------------------------------
@TRAIN_SEC_END
#DIM DYNAMIC L_I
#DIM DYNAMIC L_CNT
#DIM DYNAMIC L_REST
#DIM DYNAMIC L_T
#DIM DYNAMIC L_HIT
#DIM DYNAMIC L_EXIT

SIF SELECTCOM == [[TRAIN:行動終了]]
	RETURN

;-----------------------------
;- 退場チェック
;-----------------------------

;- 休憩者発生
FOR L_I,0,DEFTR_PL_NUM
	L_T = GTR_PL:L_I
	SIF L_T < 0
		BREAK

	SIF L_T != MASTER
		L_CNT ++

	IF TCVAR:L_T:休憩フラグ
		L_REST ++
	;体力0
	ELSEIF !BASE:L_T:体力
		TCVAR:L_T:休憩フラグ=1
		VARSET NEXTCOM

		;- TR:部位ポイントリセット
		CALL TR_POINT_RESET(L_T)

		IF L_T == MASTER
			L_EXIT=1
			CALL CP_PRINTFORMW_COLORED("【休憩】", COLOR("水色"), @"%CALLNAME:L_T%が疲れたため、調教を終える事にした…")
		ELSE
			L_REST ++
			CALL CP_PRINTFORMW_COLORED("【休憩】", COLOR("水色"), @"%CALLNAME:L_T%の行動回数が0となった…")
		ENDIF
	ENDIF
NEXT

;- 結果判定
IF !L_EXIT
	IF FLAG:行動フェーズ == [[行動フェーズ：調教]]
		;調教対象無し
		IF ( TARGET > -1 ) && ( L_CNT == L_REST )
				L_EXIT=1
				CALL CP_PRINTFORMW_COLORED("【時間】", COLOR("水色"), @"対象者が全員疲れたため、今回の調教を終える事にした…")
		;時間切れ
		ELSEIF (TFLAG:行動経過ターン数 >= TFLAG:行動最大ターン数)
				L_EXIT=1
				CALL CP_PRINTFORMW_COLORED("【時間】", COLOR("水色"), @"時刻が来たため、今回の調教を終える事にした…")
		ENDIF
	ENDIF
ENDIF


IF L_EXIT
	SELECTCOM = [[TRAIN:行動終了]]
	;マクロクリア
	VARSET NEXTCOM
ENDIF

;=================================================
;   sub TRAIN_SHOW_COM_MAIN
;=================================================
;   TR:コマンドリスト表示(主処理)
;-------------------------------------------------
@TRAIN_SHOW_COM_MAIN
#DIM DYNAMIC L_I
#DIM DYNAMIC L_TMP
#DIM DYNAMIC L_COM
#DIM DYNAMIC L_LINE
#DIM DYNAMIC L_FILTER_VAL
#DIM DYNAMIC L_CNT_PRINT
#DIM DYNAMIC L_MAX_PRINT
#DIM DYNAMIC L_SELCOM_BK

;-----------------------------
;- 初期処理
;-----------------------------
VARSET GTR_COMABLE, -1

L_COM = 0
L_CNT_PRINT	= 0
IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
	L_MAX_PRINT	= PRINTCPERLINE()
ELSE
	IF FLAG:調教画面表示モード == 2
		L_MAX_PRINT	= PRINTCPERLINE()
	ELSE
		L_MAX_PRINT	= 4
	ENDIF
ENDIF
L_LINE = LINECOUNT

;-----------------------------
;- 表示開始
;-----------------------------

;-- 実行可能コマンドを画面表示
FOR L_I, GTR_COM_POS_START, (1000 + 1)
	;- 派生は除外(500〜599)
	SIF (500 <= L_I) && (L_I < 600)
		CONTINUE

	;画像を全て出力したら回復
	SIF TFLAG:調教画像表示行数 == FLAG:最大数_調教画像行数
		L_MAX_PRINT	= PRINTCPERLINE()

	;- TR:コマンドリスト改行処理
	CALL TRAIN_SHOW_COM_CR_MAIN(L_I, 0, L_CNT_PRINT)
	SIF RESULT
		L_CNT_PRINT=0

	;- 改行処理(800以降)
	SELECTCASE L_I
		;- 前行に出力有りなら改行する項目
		CASE 800, 920
			;遭遇後、且つ、体力有りの場合のみ改行
			IF (TARGET != -1) && TCVAR:遭遇フラグ && BASE:体力
				IF !LINEISEMPTY() && L_CNT_PRINT
					CALL TRAIN_SHOW_COM_CR_EXE
					L_CNT_PRINT=0
				ENDIF
			ENDIF
		;- コマンド表示区切り
		CASE 900
			SIF !LINEISEMPTY()
				CALL TRAIN_SHOW_COM_CR_EXE
			CALL TRAIN_SHOW_COM_CR_EXE
			;900以降は空行出力
			LOCAL = 11 - ( LINECOUNT - L_LINE )
			IF LOCAL > 0
				REPEAT LOCAL
					CALL TRAIN_SHOW_COM_CR_EXE
				REND
			ENDIF
			L_CNT_PRINT=0
	ENDSELECT

	;- CHK:固定表示
	SELECTCASE L_I
		;- 調教メニュー
		CASE DEFTR_MACRO_COM_FROM TO DEFTR_MACRO_COM_TO
			IF TRAIN_SHOW_COM_ABLE(TARGET, 01)
				LOCAL    = L_I - DEFTR_MACRO_COM_POS
				LOCALS:0 = マクロ登録数{LOCAL}
				LOCALS:1 = マクロ使用フラグ{LOCAL}
				LOCALS:2 = マクロ種別{LOCAL}
				LOCALS:3 = 調メ{LOCAL}：%COM_MACRO_CATEGORY_TITLE(FLAG:(LOCALS:2))%
				SIF TARGET == MASTER
					CONTINUE
				SIF !FLAG:(LOCALS:0) || FLAG:(LOCALS:1)
					CONTINUE
				PRINTFORMC %LOCALS:3%\[{L_I}\]
			ENDIF
		;- オプション
		CASE 920
			PRINTC 能力表示(MASTER)[920]
		CASE 921
			SIF TARGET == -1
				SETCOLOR COLOR("灰色")
			PRINTC 能力表示(TARGET)[921]
			RESETCOLOR
		CASE 922
			PRINTC 能力表示(一覧)[922]
		CASE 924
			PRINTC コマンドフィルタ[924]
		CASE 925
			PRINTC コンフィグ[925]
		CASE 930 TO (930 + DEFTR_MACRO_CNT - 1)
			LOCAL = L_I - 930
			PRINTFORMC 調教メニュー{LOCAL}[{L_I}]
		CASE 990
			IF !LINEISEMPTY()
				CALL TRAIN_SHOW_COM_CR_EXE
				L_CNT_PRINT=0
			ENDIF
			CALL TRAIN_SHOW_COM_CR_EXE
			L_CNT_PRINT=0
			PRINTC 行動終了[990]
		CASE 992
			PRINTC 履歴[992]
		;- その他
		CASEELSE
			SIF TARGET == MASTER
				CONTINUE
			;- 派生になるかチェック
			TRYCCALLFORM COM_FUNC_JUMP_X{L_I}
				L_COM = ( (RESULT >= 0) ? RESULT # L_I )
			CATCH
				L_COM = L_I
			ENDCATCH

			;- コマンドに変更が無い場合、条件チェック
			IF L_COM == L_I
				SELECTCOM=L_COM

				;- TR:コマンド実行条件チェック
				CALL TR_COM_ABLE_MAIN
				SIF !RESULT
					CONTINUE
			ENDIF

			;- CHK:フィルタ
			TRYCCALLFORM COM_FUNC_GET_TYPE_X{L_COM}
				L_FILTER_VAL = RESULT

				IF FLAG:フィルタ仕事 && (L_FILTER_VAL == [[COM種別:仕事系]])
					CONTINUE
				ELSEIF FLAG:フィルタ日常 && (L_FILTER_VAL == [[COM種別:日常系]])
					CONTINUE
				ELSEIF FLAG:フィルタコミュ && (L_FILTER_VAL == [[COM種別:コミュ系]])
					CONTINUE
				ELSEIF FLAG:フィルタ愛撫 && (L_FILTER_VAL == [[COM種別:愛撫系]])
					CONTINUE
				ELSEIF FLAG:フィルタ奉仕 && (L_FILTER_VAL == [[COM種別:奉仕系]])
					CONTINUE
				ELSEIF FLAG:フィルタ道具 && (L_FILTER_VAL == [[COM種別:道具系]])
					CONTINUE
				ELSEIF FLAG:フィルタＶ性交 && (L_FILTER_VAL == [[COM種別:Ｖ性交系]])
					CONTINUE
				ELSEIF FLAG:フィルタＡ性交 && (L_FILTER_VAL == [[COM種別:Ａ性交系]])
					CONTINUE
				ELSEIF FLAG:フィルタＶ性交 && (L_FILTER_VAL == [[COM種別:逆Ｖ性交系]])
					CONTINUE
				ELSEIF FLAG:フィルタＡ性交 && (L_FILTER_VAL == [[COM種別:逆Ａ性交系]])
					CONTINUE
				ENDIF
			CATCH
			ENDCATCH

			L_TMP = L_I

			;-- hit時
			GTR_COMABLE:L_TMP = L_COM

			;- コマンド追加条件確認
			L_SELCOM_BK = SELECTCOM
			SELECTCOM = L_COM
			;特定コマンドは色有り
			SELECTCASE SELECTCOM
				CASE [[TRAIN:寝室に誘う]]
					SETCOLOR COLOR("紫")
			ENDSELECT

			TFLAG:表示無しフラグ = 1
			CALL TR_COM_ADD_ABLE_COMMON
			;- CHK:実行中止か
			IF !RESULT
				SETCOLOR COLOR("灰色")
			ENDIF
			SELECTCOM = L_SELCOM_BK
			TFLAG:表示無しフラグ = 0

			;- 取得:コマンド名称
			CALLFORM COM_FUNC_GET_NAME_X{L_COM}
			PRINTFORMC %RESULTS%[{L_TMP,3}]
			RESETCOLOR
	ENDSELECT

	L_CNT_PRINT ++
	IF (L_CNT_PRINT % L_MAX_PRINT) == 0
		CALL TRAIN_SHOW_COM_CR_EXE
		L_CNT_PRINT = 0
	ENDIF
NEXT

;- TR:画像出力処理
CALL TRAIN_SHOW_IMG


;=================================================
;   sub TRAIN_SHOW_COM_CR_MAIN
;=================================================
;   TR:コマンドリスト改行処理
;-------------------------------------------------
; Input:
;  ARG:0				コマンド番号
;  ARG:1				処理区分(0:メニュー 1:コンフィグ)
;  ARG:2				コマンド表示数
; RETURN:
;  RESULT				改行有無
;-------------------------------------------------
@TRAIN_SHOW_COM_CR_MAIN(ARG:0, ARG:1=0, ARG:2=0)
#DIM DYNAMIC L_I
#DIM DYNAMIC L_HIT
#DIM DYNAMIC L_CNT_PRINT
#DIM DYNAMIC L_FLG_ACTION_OK

L_I = ARG

IF !ARG:1
	IF (TFLAG:行動経過ターン数 >= TFLAG:行動最大ターン数) && L_I == GTR_COM_POS_START
		SIF FLAG:行動フェーズ == [[行動フェーズ：交流]]
			L_FLG_ACTION_OK = 2
	ELSEIF TRAIN_SHOW_COM_ABLE(TARGET, 01)
		L_FLG_ACTION_OK = 1
	ENDIF

	IF L_FLG_ACTION_OK == 2
;		SIF !LINEISEMPTY()
;			CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
		VARSET LOCALS
		LOCALS:0 = 　　┌──────────────────────────────────────┐
		LOCALS:1 = 　　｜[！] 主人の行動ターンが全て消費されました。追加で寝室に誘う事が可能です。   ｜
		LOCALS:2 = 　　└──────────────────────────────────────┘
		LOCALS:3 = 
		SETCOLOR COLOR("紫")
		;調教メニューは画像を全て出力
		REPEAT 4
			PRINTFORM %LOCALS:COUNT%
			CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
		REND
		RESETCOLOR
	ENDIF
	L_CNT_PRINT = ARG:2
ELSE
	L_FLG_ACTION_OK = 1
	L_CNT_PRINT = 1
ENDIF

;- 改行処理
SELECTCASE L_I
	;- コマンド種別分類
	CASE [[TRAIN:家事訓練]], [[TRAIN:口説く]], DEFTR_MACRO_COM_POS
		;交流時、且つ、遭遇後、且つ、体力有りの場合のみ改行
		IF FLAG:行動フェーズ == [[行動フェーズ：交流]] && L_FLG_ACTION_OK == 1
			SIF !LINEISEMPTY() && L_CNT_PRINT
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
			IF L_I == [[TRAIN:家事訓練]]
				SETCOLOR COLOR("薄緑")
				PRINT [交流]
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
			ELSEIF L_I == [[TRAIN:口説く]]
				SETCOLOR COLOR("紫")
				PRINT [調教]
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
			ELSEIF L_I == DEFTR_MACRO_COM_POS
				SETCOLOR COLOR("紫")
				PRINT [調メ]
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
			ENDIF
			RESETCOLOR
			L_HIT=1
		ENDIF
		;調教時、且つ、遭遇後、且つ、体力有りの場合のみ改行
		IF FLAG:行動フェーズ == [[行動フェーズ：調教]] && L_FLG_ACTION_OK == 1
			SIF !LINEISEMPTY() && L_CNT_PRINT
				CALL TRAIN_SHOW_COM_CR_EXE

			IF L_I == DEFTR_MACRO_COM_POS
				IF FLAG:調教画面表示モード != 2
					;---------------------------------- 案1
					;;調教メニューは画像を全て出力してから表示
					;WHILE (FLAG:最大数_調教画像行数 - TFLAG:調教画像表示行数)
					;	CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
					;WEND
					;SIF !LINEISEMPTY()
					;	CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
					;---------------------------------- 案2
					SIF !LINEISEMPTY() && L_CNT_PRINT
						CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
					;----------------------------------
				ENDIF
				SETCOLOR COLOR("紫")
				PRINT [調メ]
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
			ENDIF
			RESETCOLOR
			L_HIT=1
		ENDIF
	;- 前行に出力有りなら改行する項目
	CASE [[TRAIN:テレビを見る]] , [[TRAIN:正常位]]
		;遭遇後、且つ、体力有りの場合のみ改行
		IF (TARGET != -1) && TCVAR:遭遇フラグ && BASE:体力
			IF !LINEISEMPTY() && L_CNT_PRINT
				CALL TRAIN_SHOW_COM_CR_EXE(ARG:1)
				L_HIT=1
			ENDIF
		ENDIF
ENDSELECT

RETURN L_HIT

;=================================================
;   sub TRAIN_SHOW_COM_CR_EXE
;=================================================
;   TR:コマンドリスト改行実行
;-------------------------------------------------
; Input:
;  ARG:0				処理区分(0:メニュー 1:コンフィグ)
;-------------------------------------------------
@TRAIN_SHOW_COM_CR_EXE(ARG=0)
PRINTL
;SIF FLAG:行動フェーズ != [[行動フェーズ：調教]]
;	RETURN
;SIF FLAG:調教画面表示モード == 2
;	RETURN
IF TFLAG:調教画像表示行数 < FLAG:最大数_調教画像行数
	CALL C_IMG_CHARA_ICON(TFLAG:調教画像表示対象者, TFLAG:調教画像表示行数, TFLAG:調教画像表示番号)
	TFLAG:調教画像表示行数 ++
	;PRINT 　　 
ELSE
	SIF ARG
		RETURN
	IF LINEISEMPTY()
		IF ( FLAG:行動フェーズ == [[行動フェーズ：交流]] ) || ( FLAG:調教画面表示モード == 2 )
			PRINTFORM %"　" * DEFTR_IMG_SPACE_MEET%
		ELSE
			PRINTFORM %"　" * DEFTR_IMG_SPACE_TRAIN%
		ENDIF
	ENDIF
ENDIF


;=================================================
;   sub TRAIN_SHOW_IMG
;=================================================
;   TR:画像出力処理
;-------------------------------------------------
@TRAIN_SHOW_IMG
IF FLAG:行動フェーズ == [[行動フェーズ：調教]]
	IF FLAG:調教画面表示モード != 2
		;調教メニューは画像を全て出力
		WHILE (FLAG:最大数_調教画像行数 - TFLAG:調教画像表示行数)
			CALL TRAIN_SHOW_COM_CR_EXE
		WEND
	ENDIF
ENDIF

;=================================================
;   sub TRAIN_SHOW_COM_ABLE
;=================================================
;   TR:コマンド出力チェック
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				チェック区分
; ・01					01:要対象、要体力
; Output:
;  RETURN				結果
;-------------------------------------------------
@TRAIN_SHOW_COM_ABLE(ARG:0, ARG:1=0)
#FUNCTION
RESULT = 0

SELECTCASE ARG:1
	CASE 01
		;要対象、要体力
		SIF (TARGET != -1) && TCVAR:遭遇フラグ && BASE:体力 && (TFLAG:行動経過ターン数 < TFLAG:行動最大ターン数)
			RETURNF 1
ENDSELECT
RETURNF 0


;-------------------------------------------------------------------------------
;   処理系(詳細)
;-------------------------------------------------------------------------------

;=================================================
;   sub TRAIN_COM_EXE
;=================================================
;   TR:コマンド実行
;-------------------------------------------------
; Output:
;  RETURN				処理結果(1:コマンド実行 0:コマンド中止)
;-------------------------------------------------
@TRAIN_COM_EXE
#DIM DYNAMIC L_TMP
#DIM DYNAMIC L_I

L_TMP=0

;-----------------------------
;- コマンド情報取得
;-----------------------------

;- 取得:コマンド名称
CALLFORM COM_FUNC_GET_NAME_X{SELECTCOM}
TSTR:実行コマンド名 = %RESULTS%

;-----------------------------
;- 実行確認
;-----------------------------

;- コマンド実行確認
TRYCCALLFORM COM_FUNC_INPUT_X{SELECTCOM}
	;- CHK:実行中止か
	IF !RESULT
		RETURN 0
	ENDIF
CATCH
ENDCATCH

;- コマンド追加条件確認
CALL TR_COM_ADD_ABLE_COMMON
;- CHK:実行中止か
IF !RESULT
	RETURN 0
ENDIF

;-------------------------------------------------
;--- 実行される為、各種後続処理を実行
;-------------------------------------------------

;-----------------------------
;- 初期化処理
;-----------------------------

;- 全キャラ
REPEAT CHARANUM
	;- ポイント更新
	TEQUIP:COUNT:ポイント日常前回 = TEQUIP:COUNT:ポイント日常
	TEQUIP:COUNT:ポイントキス前回 = TEQUIP:COUNT:ポイントキス
	TEQUIP:COUNT:ポイント愛撫前回 = TEQUIP:COUNT:ポイント愛撫
	TEQUIP:COUNT:ポイント奉仕前回 = TEQUIP:COUNT:ポイント奉仕
	TEQUIP:COUNT:ポイント性交前回 = TEQUIP:COUNT:ポイント性交

	;- ポイントクリア
	TEQUIP:COUNT:ポイント日常 = 0
	TEQUIP:COUNT:ポイントキス = 0
	TEQUIP:COUNT:ポイント愛撫 = 0
	TEQUIP:COUNT:ポイント奉仕 = 0
;	TEQUIP:COUNT:ポイント性交 = 0

	SIF !CFLAG:COUNT:行動対象フラグ && MASTER != COUNT
		CONTINUE

	;- 初期化
	VARSET SOURCE:COUNT:0, 0
	VARSET DOWNBASE:COUNT:0, 0
	VARSET CUP:COUNT:0, 0
	VARSET CDOWN:COUNT:0, 0

REND

;- その他
VARSET GTR_EXPUP:0:0
VARSET GTR_JUELUP:0:0

TFLAG:メインターゲット = TARGET

;PLAYERとTARGETは自動で行動済み
TCVAR:PLAYER:行動済みフラグ = 1
TCVAR:TARGET:行動済みフラグ = 1

;-----------------------------
;- 実行
;-----------------------------

;- コマンド実行
CALLFORM COM_FUNC_EXE_X{SELECTCOM}

;前回とコマンドが同じで、ターゲットも同じ
SIF (PREVCOM == SELECTCOM) && (TARGET == TFLAG:メインターゲット)
	TFLAG:同一コマンド実行フラグ = 1

;-----------------------------
;- カウンター
;-----------------------------


;-----------------------------
;- ソース計算
;-----------------------------

;- サブコマンド:実行(主処理)
CALL SUB_COM_EXE_MAIN

;- SOURCE反映:主処理
CALL SOURCE_CHECK_MAIN

;- TR:ムード
CALL TR_CALC_MOOD


;-------------------------------------------------
;--- ソース計算終了後、各口上・情景・計算結果を表示
;-------------------------------------------------

;-----------------------------
;- 情景:コマンド
;-----------------------------
;<ERA2WEBUI>
CALL OUTPUT_TXT("TRAIN")
;</ERA2WEBUI>
;- 表示:ページヘッダ
CALL C_LINE
;PRINTL

;- サブコマンド:メッセージ(主処理)
CALL SUB_COM_MSG_MAIN

SIF RESULT
	PRINTL

;-----------------------------
;- 情景:終了処理
;-----------------------------

;- LOG出力:絶頂(主処理)
CALL LOG_ORGASM_MAIN

;- LOG出力:絶頂(情景 - 主処理)
CALL LOG_ORGASM_SIGHT_MAIN

;- TR:パラメータ変動(刻印)
CALL TR_PALAMCHG_MARK_MAIN

;- TR:パラメータ変動(既成事実)
CALL TR_PALAMCHG_FACT_MAIN

PRINTFORML …
PRINTFORML ……
PRINTFORML ………
DRAWLINE
WAIT

;- TR:履歴
CALL TR_LOG(0)

;- TR:難易度情報
CALL TR_INFO_DIFFICULTY_LEVEL
WAIT

;-----------------------------
;- コマンド実行付随処理
;-----------------------------

;- 全キャラ
REPEAT CHARANUM
	IF COUNT == MASTER
	ELSEIF !CFLAG:COUNT:行動対象フラグ
		CONTINUE
	ENDIF

	;全員
	SIF TEQUIP:COUNT:Ｖ器具
		TEQUIP:COUNT:Ｖ器具経過ターン ++
	SIF TEQUIP:COUNT:Ａ器具
		TEQUIP:COUNT:Ａ器具経過ターン ++

REND


;-----------------------------
;- コマンド実行終了時初期化処理
;-----------------------------
;- 全キャラ
REPEAT CHARANUM
	IF COUNT == MASTER
	ELSEIF !CFLAG:COUNT:行動対象フラグ
		CONTINUE
	ENDIF

	;- POSE:初期化
	CALL POSE_CLEAR(COUNT)

	;- TCVAR初期化
	FOR L_I, 0, 50
		TCVAR:COUNT:L_I=0
	NEXT

REND

;- TFLAG初期化
REPEAT 50
	TFLAG:COUNT=0
REND

PREVCOM = SELECTCOM


;-----------------------------
;- コマンド実行終了時処理
;-----------------------------
TFLAG:行動経過ターン数 ++

RETURN 1


;=================================================
;   sub TR_CALC_MOOD
;=================================================
;   TR:ムード
;-------------------------------------------------
@TR_CALC_MOOD
#DIM L_I
#DIM L_P
#DIM L_TMP
VARSET LOCAL

L_I=0
L_TMP=0

;-----------------------------
;- 計算:ムード
;-----------------------------

;- 
FOR L_I, 0, DEFTR_PL_NUM
	L_P = GTR_PL:L_I

	SIF L_P < 0
		BREAK
	SIF L_P == MASTER
		CONTINUE

	;-- 計算
	LOCAL = 0

	;-----------------------------
	;--- プラス補正
	;-----------------------------

	;-- 個人PALAM

	;各種快感、対人ポジティブ
	LOCAL += TCVAR:L_P:絶頂加算値
	LOCAL += CUP:L_P:好意
	LOCAL += CUP:L_P:屈服
	LOCAL += CUP:L_P:欲情

	;- 各種能力で増加
	L_TMP = 0
	L_TMP += ABL:L_P:既成事実
	L_TMP += ABL:L_P:性奴隷
	L_TMP += ABL:L_P:親密
	L_TMP += ABL:L_P:従順
	L_TMP += ABL:L_P:奉仕精神
	L_TMP = 100 + (10 * L_TMP)

	LOCAL = LOCAL * L_TMP / 100

	;-----------------------------
	;--- マイナス補正
	;-----------------------------

	;-- 個人PALAM
	LOCAL -= CUP:L_P:苦痛
	LOCAL -= CUP:L_P:抑鬱

	;-- 対人PALAM
	LOCAL -= CUP:L_P:恐怖
	LOCAL -= CUP:L_P:反感
	LOCAL -= CUP:L_P:不快

	;-----------------------------
	;--- 計上
	;-----------------------------

	;-- 丸め補正(1/100)
	LOCAL /= 100

	;一定以下は最低値設定
	IF LOCAL <= 5
		IF DOWNBASE:L_P:体力
			LOCAL = 5
		ELSE
			LOCAL = 0
		ENDIF
	ENDIF

	DOWNBASE:L_P:ムード = LOCAL

	BASE:L_P:ムード += DOWNBASE:L_P:ムード
	BASE:L_P:ムード = MAX(BASE:L_P:ムード,0)
	BASE:L_P:ムード = MIN(BASE:L_P:ムード,MAXBASE:L_P:ムード)

NEXT


;=================================================
;   sub TR_LOST_VIRGIN
;=================================================
;   TR:初体験
;-------------------------------------------------
; Input:
;  ARG:0				対象者
; Output:
;  RETURN				結果
;-------------------------------------------------
@TR_LOST_VIRGIN(ARG)
#DIM L_HIT
#DIM L_BAK

L_BAK = TARGET
TARGET = ARG

L_HIT=0

IF TCVAR:ARG:履歴_EXP_処女喪失
	PRINTFORM %CALLNAME:ARG%
	SETCOLOR COLOR("ピンク")
	PRINTW 処女喪失
	RESETCOLOR
	IF ARG != MASTER
		;- 口上出力:処女喪失時
		CALLFORM KOJO_SYS_LOST_VIRGIN([[口上:処女喪失]])
	ENDIF
	L_HIT=1
ENDIF

IF TCVAR:ARG:履歴_EXP_Ａ処女喪失
	PRINTFORM %CALLNAME:ARG%
	SETCOLOR COLOR("ピンク")
	PRINTW Ａ処女喪失
	RESETCOLOR
	IF ARG != MASTER
		;- 口上出力:処女喪失時
		CALLFORM KOJO_SYS_LOST_VIRGIN([[口上:Ａ処女喪失]])
	ENDIF
	L_HIT=1
ENDIF

IF TCVAR:ARG:履歴_EXP_童貞喪失
	PRINTFORM %CALLNAME:ARG%
	SETCOLOR COLOR("群青")
	PRINTW 童貞喪失
	RESETCOLOR
	IF ARG != MASTER
		;- 口上出力:処女喪失時
		CALLFORM KOJO_SYS_LOST_VIRGIN([[口上:童貞喪失]])
	ENDIF
	L_HIT=1
ENDIF

;- 初体験記録_TRAIN
CALL 初体験記録_TRAIN(ARG)

;初期化
TCVAR:ARG:履歴_EXP_処女喪失 = 0
TCVAR:ARG:履歴_EXP_Ａ処女喪失 = 0
TCVAR:ARG:履歴_EXP_童貞喪失 = 0

TARGET = L_BAK

RETURN L_HIT

;=================================================
;   sub TR_SAME_COMMAND 
;=================================================
;   TR:同一コマンド
;-------------------------------------------------
; Input:
;  ARG:0				モード(0:W 1:L)
;-------------------------------------------------
@TR_SAME_COMMAND(ARG=1)

IF TFLAG:同一コマンド実行フラグ
	SETCOLOR COLOR("水色")
	PRINTL ＜同一コマンド実行＞
	RESETCOLOR
	SIF !ARG
		WAIT
ENDIF


;=================================================
;   sub TR_MSG_START
;=================================================
;   TR:開始メッセージ
;-------------------------------------------------
@TR_MSG_START
#DIM L_I
#DIM L_T
#DIM L_BAK_TGT
#DIM L_CNT
#DIMS L_PARTY
L_PARTY = %TR_PARTY_NAME()%

DRAWLINE
PRINTFORML 【館内 %GET_AREA_NAME()% - %INFO_TIME()%】
IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
	PRINTFORML %GET_AREA_NAME()%には%L_PARTY%がいる……

	;- 口上
	L_BAK_TGT = TARGET
	FOR L_I, 0, DEFTR_PL_NUM
		L_T = GTR_PL:L_I
		SIF L_T < 0
			BREAK
		SIF L_T == MASTER
			CONTINUE

		;- TR:ターゲット設定
		CALL TR_TGT_SET(L_T, 1)

		;- 口上:エキストラモード
		CALL KOJO_SYS_EXTRA
	NEXT
	TARGET = L_BAK_TGT

	PRINTFORML 館内の行動を開始しよう……
ELSE
	ALIGNMENT CENTER
	LOCAL = DEFTR_IMG_MAX_TRAIN
	REPEAT LOCAL
		;あなたを入れる事でDUMMY扱い
		CALL C_IMG_CHARA_ICON(0, COUNT, DEFTR_IMG_NO_TRAIN, LOCAL, 1)
		PRINTL
	REND
	ALIGNMENT LEFT

	PRINTFORML %CALLNAME:MASTER%は%L_PARTY%を寝室に集め、調教する事にした……
	FORCEWAIT
	PRINTL 

	;- 口上
	L_BAK_TGT = TARGET
	FOR L_I, 0, DEFTR_PL_NUM
		L_T = GTR_PL:L_I
		SIF L_T < 0
			BREAK
		SIF L_T == MASTER
			CONTINUE

		;- TR:ターゲット設定
		CALL TR_TGT_SET(L_T, 1)

		CFLAG:主人初調教  = 1
		;- 口上:遭遇時
		CALL KOJO_SYS_ENCOUNTER("START_TRAIN")

		CFLAG:最終調教日  = DAY
	NEXT
	TARGET = L_BAK_TGT
	PRINTFORMW %CALLNAME:MASTER%は%L_PARTY%を押し倒し、調教を開始した……
ENDIF
PRINTL
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …



;=================================================
;   sub TR_MSG_END
;=================================================
;   TR:終了メッセージ
;-------------------------------------------------
@TR_MSG_END
#DIM L_I
#DIM L_T
#DIM L_BAK_TGT
#DIMS L_PARTY
L_PARTY = %TR_PARTY_NAME()%

;表示:ページヘッダ
CALL C_LINE

IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
	PRINTFORMW %CALLNAME:MASTER%は館内の探索を終えた……
ELSE
	PRINTFORMW %CALLNAME:MASTER%は%L_PARTY%の調教を終えた……
	;- 口上
	L_BAK_TGT = TARGET
	FOR L_I, 0, DEFTR_PL_NUM
		L_T = GTR_PL:L_I
		SIF L_T < 0
			BREAK
		SIF L_T == MASTER
			CONTINUE

		;- TR:ターゲット設定
		CALL TR_TGT_SET(L_T, 1)

		;- NTR:計算
		CALL NTR_CALC(L_T, -50)

		;反発刻印LV3
		IF MARK:反発刻印 == 3
			PRINTFORMW %CALLNAME%は%CALLNAME:MASTER%に不満の視線を送っている…
		;性奴隷LV0
		ELSEIF !ABL:性奴隷
			PRINTFORMW %CALLNAME%は%CALLNAME:MASTER%を見ている…
		ELSE
			PRINTFORMW %CALLNAME%は頬を紅潮させている…
		ENDIF

		;- 口上:遭遇時
		CALL KOJO_SYS_ENCOUNTER("END")
	NEXT

	;- TR:ターゲット設定
	CALL TR_TGT_SET(L_BAK_TGT, 1)
	PRINTFORMW %L_PARTY%と会話をしながら休憩した……
	PRINTL
	DRAWLINE
ENDIF
PRINTL
PRINTFORML ………
PRINTFORML ……
PRINTFORML …
FORCEWAIT
PRINTL

;- SRC表示:JUEL(主処理)
CALL SRC_SHOW_JUEL_MAIN

PRINTFORML …
PRINTFORML ……
PRINTFORMW ………
PRINTL

;=================================================
;   sub TR_LOG
;=================================================
;   TR:履歴
;-------------------------------------------------
; Intput:
;  ARG:0				WAITフラグ
;-------------------------------------------------
@TR_LOG(ARG=1)

PRINTL

;- TR:同一コマンド
CALL TR_SAME_COMMAND

;- SRC表示:SOURCE(主処理)
CALL SRC_SHOW_SRC_MAIN

;- SRC表示:BASE-EXP(主処理)
CALL SRC_SHOW_BASE_EXP_MAIN

;- SRC表示:JUEL(主処理)
CALL SRC_SHOW_JUEL_MAIN(1)

;- SRC表示:MONEY(主処理)
CALL SRC_SHOW_MONEY_MAIN

;- SRC表示:CUP(主処理)
CALL SRC_SHOW_CUP_MAIN
SIF ARG
	WAIT

;=================================================
;   sub TR_AUTO_COM
;=================================================
;   TR:自動コマンド
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@TR_AUTO_COM

SIF TARGET == -1
	RETURN 0

SIF !BASE:TARGET:体力 || (TFLAG:行動経過ターン数 >= TFLAG:行動最大ターン数)
	VARSET NEXTCOM

SIF !NEXTCOM:0
	RETURN 0

SELECTCOM = NEXTCOM:0
ARRAYREMOVE NEXTCOM, 0, 1

RETURN 1

;=================================================
;   sub TR_START_SET
;=================================================
;   TR:開始状態セット
;-------------------------------------------------
@TR_START_SET
#DIM L_I
#DIM L_P
#DIM L_MOOD
#DIM L_VAL

FOR L_I, 0, CHARANUM
	L_P = L_I

	SIF L_P < 0
		BREAK
	SIF L_P == MASTER
		CONTINUE

	L_MOOD = 0
	IF TALENT:L_P:上位陥落
		L_MOOD = 3
	ELSEIF TALENT:L_P:合意 || TALENT:L_P:恋慕
		L_MOOD = 2
	ELSEIF FLAG:時間帯 == [[時間帯:夜]]
		L_MOOD = 2
	ELSEIF FLAG:時間帯 == [[時間帯:夕]]
		L_MOOD = 1
	ELSEIF TALENT:L_P:好意
		L_MOOD = 1
	ENDIF
	BASE:L_P:ムード = (FLAG:最大数_ムード / FLAG:最大数_ムード桁数) * L_MOOD

NEXT

;=================================================
;   sub TR_INFO_DIFFICULTY_LEVEL
;=================================================
;   TR:難易度情報
;-------------------------------------------------
@TR_INFO_DIFFICULTY_LEVEL
#DIM DYNAMIC L_FLG

VARSET L_FLG

IF !CFLAG:初回遭遇フラグ
	CFLAG:初回遭遇フラグ = 1
	IF ( TALENT:貞操観念 || TALENT:一線越えない ) && !TALENT:信頼
		SETCOLOR COLOR("ピンク")
		PRINTFORM 【高難度】
		RESETCOLOR
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:貞操観念
			SETCOLOR COLOR("紫")
			PRINTFORM 【貞操観念】
			RESETCOLOR
			L_FLG = 1
		ENDIF
		IF TALENT:一線越えない
			SIF L_FLG
				PRINTFORM と
			SETCOLOR COLOR("紫")
			PRINTFORM 【一線越えない】
			RESETCOLOR
			L_FLG = 1
		ENDIF
		PRINTFORML を所持している為、ガードが堅い……
		PRINTFORM           %CALLNAME:TARGET%の抱える問題の解決に協力し、
		SETCOLOR COLOR("紫")
		PRINTFORM 【信頼】
		RESETCOLOR
		PRINTFORML を獲得すると攻略が容易となるだろう……
		PRINTW
		PRINTW …
		PRINTW ……
		PRINTL ………
	ENDIF
ENDIF


;=================================================
;   sub TR_STAY_ACTION_SET
;=================================================
;   TR:滞在行動設定
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				処理(0:初期設定 1:情景)
; Output:
;  RESULTS:0			行動名
;  RESULTS:1			行動情景
;-------------------------------------------------
@TR_STAY_ACTION_SET(ARG:0, ARG:1)
#DIM  DYNAMIC L_P
#DIM  DYNAMIC L_VAL
#DIM  DYNAMIC L_VAL_2
#DIM  DYNAMIC L_COM_TYPE
#DIMS DYNAMIC L_COM_NAME
#DIMS DYNAMIC L_STR, 2
#DIMS DYNAMIC L_SCENE, 10

L_P = ARG
VARSET L_VAL
VARSET L_VAL_2
VARSET L_STR
VARSET RESULTS

SIF L_P < 0
	RETURN
SIF L_P == MASTER
	RETURN
SIF !CFLAG:L_P:行動対象フラグ
	RETURN

;結果反映
IF ARG:1 == 0
	IF FLAG:行動フェーズ == [[行動フェーズ：交流]]
		IF FLAG:時間帯 == [[時間帯:夜]]
			L_VAL_2 = RAND:2
		ELSE
			L_VAL_2 = RAND:4
		ENDIF

		IF TALENT:L_P:喫煙 && !L_VAL_2
			L_VAL   = [[滞在行動:喫煙中]]
		ELSEIF TALENT:L_P:飲酒 && !L_VAL_2
			L_VAL   = [[滞在行動:飲酒中]]
		ELSE
			SELECTCASE RAND:5
				CASE 4
					L_VAL   = [[滞在行動:休憩中]]
				CASE 3
					L_VAL   = [[滞在行動:鍛錬中]]
				CASE 2
					L_VAL   = [[滞在行動:読書中]]
				CASE 1
					L_VAL   = [[滞在行動:喫茶中]]
				CASEELSE
					L_VAL   = [[滞在行動:寛ぎ中]]
			ENDSELECT
		ENDIF
	ELSE
		L_VAL   = [[滞在行動:調教中]]
	ENDIF

	TCVAR:L_P:滞在行動 = L_VAL
ELSE
	SELECTCASE TCVAR:L_P:滞在行動
		CASE [[滞在行動:寛ぎ中]]
			L_STR:0 = 寛ぎ中
			L_STR:1 = %CALLNAME:L_P%はソファーで寛いでいる…
		CASE [[滞在行動:喫茶中]]
			L_STR:0 = 喫茶中
			L_STR:1 = %CALLNAME:L_P%はティータイムを楽しんでいる…
		CASE [[滞在行動:喫煙中]]
			L_STR:0 = 喫煙中
			L_STR:1 = %CALLNAME:L_P%はタバコを吹かしている…
		CASE [[滞在行動:飲酒中]]
			L_STR:0 = 飲酒中
			L_STR:1 = %CALLNAME:L_P%は酒を飲んで過ごしている…
		CASE [[滞在行動:読書中]]
			L_STR:0 = 読書中
			L_STR:1 = %CALLNAME:L_P%は本を読んで過ごしている…
		CASE [[滞在行動:鍛錬中]]
			L_STR:0 = 鍛錬中
			L_STR:1 = %CALLNAME:L_P%はトレーニングをして過ごしている…
		CASE [[滞在行動:休憩中]]
			L_STR:0 = 休憩中
			L_STR:1 = %CALLNAME:L_P%は仕事の休憩を取っている…
		CASE [[滞在行動:調教中]]
			L_STR:0 = 調教中

			;- TR情報:コマンド名
			CALL TR_INFO_COM_NAME(ARG)
			L_COM_NAME = RESULTS

			TRYCALLFORM COM_FUNC_GET_TYPE_X{RESULT}
			L_COM_TYPE = RESULT

			;タイプ別
			SELECTCASE L_COM_TYPE
				CASE [[COM種別:愛撫系]]
				CASE [[COM種別:道具系]]
				CASE [[COM種別:奉仕系]]
				CASE [[COM種別:Ｖ性交系]], [[COM種別:Ａ性交系]]
					LOCALS = \@ L_COM_TYPE == [[COM種別:Ｖ性交系]] ? 秘裂 # 後ろの穴 \@
					SELECTCASE RESULTS
						CASE "正常位"
							L_STR:1 = %CALLNAME:L_P%は正常位で%LOCALS%を突かれながら両胸を揺らしている…
						CASE "後背位"
							L_SCENE:0 = %CALLNAME:L_P%は後ろから%LOCALS%を突かれながら両胸を揺らしている…
							L_SCENE:1 = %CALLNAME:L_P%は後ろから両腕を持たれながらペニスで%LOCALS%を突かれている…
							L_STR:1 = %L_SCENE:(RAND:2)%
						CASE "騎乗位"
							L_SCENE:0 = %CALLNAME:L_P%は%CALLNAME:MASTER%の上に乗って%LOCALS%で繋がりながら両胸を揺らしている…
							L_SCENE:1 = %CALLNAME:L_P%は%CALLNAME:MASTER%の上に乗って%LOCALS%でペニスを咥え込んで腰を振っている…
							L_STR:1 = %L_SCENE:(RAND:2)%
						CASE "対面座位"
							L_STR:1 = %CALLNAME:L_P%は%CALLNAME:MASTER%に前から座り%LOCALS%をペニスで貫かれている…
						CASE "背面座位"
							L_STR:1 = %CALLNAME:L_P%は%CALLNAME:MASTER%に後ろ向きで座り%LOCALS%をペニスで貫かれている…
					ENDSELECT
				CASE [[COM種別:逆Ｖ性交系]], [[COM種別:逆Ａ性交系]]
					LOCALS = \@ L_COM_TYPE == [[COM種別:逆Ｖ性交系]] ? 秘裂 # 後ろの穴 \@
					SELECTCASE RESULTS
						CASE "逆正常位"
							L_STR:1 = %CALLNAME:MASTER%は%CALLNAME:L_P%に正常位で%LOCALS%を突かれながら両胸を揺らしている…
						CASE "逆後背位"
							L_SCENE:0 = %CALLNAME:MASTER%は%CALLNAME:L_P%に後ろから%LOCALS%を突かれながら両胸を揺らしている…
							L_SCENE:1 = %CALLNAME:MASTER%は%CALLNAME:L_P%に後ろから両腕を持たれながらペニスで%LOCALS%を突かれている…
							L_STR:1 = %L_SCENE:(RAND:2)%
						CASE "逆騎乗位"
							L_SCENE:0 = %CALLNAME:MASTER%は%CALLNAME:L_P%の上に乗って%LOCALS%で繋がりながら両胸を揺らしている…
							L_SCENE:1 = %CALLNAME:MASTER%は%CALLNAME:L_P%の上に乗って%LOCALS%でペニスを咥え込んで腰を振っている…
							L_STR:1 = %L_SCENE:(RAND:2)%
						CASE "逆対面座位"
							L_STR:1 = %CALLNAME:MASTER%は%CALLNAME:L_P%に前から座り%LOCALS%をペニスで貫かれている…
						CASE "逆背面座位"
							L_STR:1 = %CALLNAME:MASTER%は%CALLNAME:L_P%に後ろ向きで座り%LOCALS%をペニスで貫かれている…
					ENDSELECT
				CASE [[COM種別:ＳＭ系]]
				;その他
				CASEELSE
			ENDSELECT

			IF L_STR:1 == ""
				IF !BASE:L_P:体力
					L_STR:1 = %CALLNAME:L_P%はベッドの上で休んでいる…
				ELSE
					L_STR:1 = %CALLNAME:L_P%はベッドの上で裸となり、調教を受けている…
				ENDIF
			ENDIF

	ENDSELECT

	CSTR:L_P:滞在行動名称 = %L_STR:0%
	CSTR:L_P:滞在行動内容 = %L_STR:1%
ENDIF

